dnl Process this file with autoconf to produce a configure script. -*-m4-*-
AC_INIT(src/ls.c)
AM_CONFIG_HEADER(config.h)
AC_CANONICAL_HOST

AM_INIT_AUTOMAKE([fileutils], [4.1])

ALL_LINGUAS="cs da de el es fr gl it ja ko nl no pl pt pt_BR ru sk sl sv zh"

jm_PERL
AC_PROG_CC
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_RANLIB
AC_AIX
AC_MINIX

jm_MACROS

AM_HEADER_TIOCGWINSZ_NEEDS_SYS_IOCTL
jm_WINSIZE_IN_PTEM

# Check for SunOS statfs brokenness wrt partitions 2GB and larger.
# If <sys/vfs.h> exists and struct statfs has a member named f_spare,
# enable the work-around code in fsusage.c.
AC_MSG_CHECKING([for statfs that truncates block counts])
AC_CACHE_VAL(fu_cv_sys_truncating_statfs,
[AC_TRY_COMPILE([
#if !defined(sun) && !defined(__sun)
choke -- this is a workaround for a Sun-specific problem
#endif
#include <sys/types.h>
#include <sys/vfs.h>],
[struct statfs t; long c = *(t.f_spare);],
fu_cv_sys_truncating_statfs=yes,
fu_cv_sys_truncating_statfs=no,
)])
if test $fu_cv_sys_truncating_statfs = yes; then
  AC_DEFINE(STATFS_TRUNCATES_BLOCK_COUNTS, 1,
[  Define if the block counts reported by statfs may be truncated to 2GB
   and the correct values may be stored in the f_spare array.
   (SunOS 4.1.2, 4.1.3, and 4.1.3_U1 are reported to have this problem.
   SunOS 4.1.1 seems not to be affected.)])
fi
AC_MSG_RESULT($fu_cv_sys_truncating_statfs)

jm_LIB_CHECK

AM_GNU_GETTEXT

# This is necessary so that .o files in LIBOBJS are also built via
# the ANSI2KNR-filtering rules.
LIBOBJS=`echo $LIBOBJS|sed 's/\.o /\$U.o /g;s/\.o$/\$U.o/'`

AC_OUTPUT([Makefile
           doc/Makefile
	   intl/Makefile
	   lib/Makefile
	   man/Makefile
	   m4/Makefile
	   po/Makefile.in
	   src/Makefile
	   tests/Makefile
	   tests/chgrp/Makefile
	   tests/chmod/Makefile
	   tests/cp/Makefile
	   tests/dd/Makefile
	   tests/du/Makefile
	   tests/dircolors/Makefile
	   tests/install/Makefile
	   tests/ln/Makefile
	   tests/ls/Makefile
	   tests/ls-2/Makefile
	   tests/mkdir/Makefile
	   tests/mv/Makefile
	   tests/rm/Makefile
	   tests/rmdir/Makefile
	   tests/shred/Makefile
	   tests/touch/Makefile
	  ])
