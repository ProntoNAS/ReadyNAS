dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/noflushd.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(noflushd,2.8)

AM_MAINTAINER_MODE

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h syslog.h unistd.h sys/ioctl.h sys/time.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_ST_RDEV

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_HEADER_MAJOR
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(strdup)

dnl Define our own install targets.
docdir=""
initdir=""
scheme=""

AC_ARG_WITH(scheme, 
[  --with-scheme=<dist>      adapt to distribution specifics. 
                             <dist> may be one of debian, redhat or suse],
[
	case "$withval" in
		debian)
			scheme="debian"
			;;
		redhat)
			scheme="redhat"
			;;
		suse)
			scheme="suse"
			;;
		*)
			AC_MSG_ERROR([Unknown dist layout $withval])
	esac
],
[
	AC_MSG_CHECKING([for distribution layout])
	if test -d /sbin/init.d/; then
		scheme="suse"
	elif test -d /etc/rc.d/init.d; then
		scheme="redhat"
	elif test -d /etc/init.d; then
		scheme="debian"
	else
		scheme="unknown"
	fi
	AC_MSG_RESULT([$scheme])
]
)

dnl This is somewhat half-hearted. We install into /usr/local by default
dnl as we should. Nevertheless we use the distribution's doc dir layout.
dnl Anyway, initdir is a fix place, even if we install into /usr/local.
case "$scheme" in
	debian)
		docdir='${exec_prefix}'/share/doc/$PACKAGE/
		initdir=/etc/init.d/
		;;
	redhat)
		docdir='${exec_prefix}'/doc/$PACKAGE-$VERSION/
		initdir=/etc/rc.d/init.d/
		;;
	suse)
		docdir='${exec_prefix}'/doc/packages/$PACKAGE/
		initdir=/sbin/init.d/
		;;
esac

AC_ARG_WITH(docdir,
[  --with-docdir=<dir>       Directory to put documentation files in.],
[
	docdir="$withval"
],)
AC_ARG_WITH(initdir,
[  --with-initdir=<dir>      Directory to put init skipt in.],
[
	initdir="$withval"
],)

if test -z "$docdir"; then
	docdir='${exec_prefix}'/doc/$PACKAGE/
	AC_MSG_WARN([Setting docdir to $docdir. No better location found.])
fi

if test -z "$initdir"; then
	initdir="$docdir"
	AC_MSG_WARN([Will not install init skript. An example, however, can be found in $docdir.])
fi
 
AC_SUBST(docdir)
AC_SUBST(initdir)

AC_OUTPUT(Makefile src/Makefile man/Makefile skripts/Makefile)
